# Maintainer: Vincent Grande <shoober420@gmail.com>
# Contributor: Det <thatone>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=xf86-input-libinput-git-oldherl
pkgver=0.30.0.6.r275.g8ebe495
pkgrel=1
pkgdesc="Generic input driver for the X.Org server based on libinput. With patch for setting scroll scale."
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libinput')
makedepends=('git' 'xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.1' 'libxi' 'libx11' 'xorgproto')
conflicts=('xorg-server<1.19.0' 'X-ABI-XINPUT_VERSION<24' 'X-ABI-XINPUT_VERSION>=25')
provides=('xf86-input-libinput')
conflicts=('xf86-input-libinput')
source=("git://anongit.freedesktop.org/xorg/driver/xf86-input-libinput" "0001-scroll-scale.patch")
sha512sums=('SKIP' 'SKIP')

prepare() {
  cd xf86-input-libinput
  git config user.name "builduser"
  git config user.email "builduser@example.com"
  git am ../0001-scroll-scale.patch
}

pkgver() {
  cd xf86-input-libinput
  
  echo $(git describe --long | cut -d "-" -f4-5 | tr - .).r$(git rev-list HEAD --count).$(git describe --long | cut -d "-" -f6)
}

build() {
  cd xf86-input-libinput

  ./autogen.sh  --prefix=/usr \
                --disable-static
  make
}

package() {
  cd xf86-input-libinput

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xf86-input-libinput"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xf86-input-libinput/"
}
